!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!ANSYS 11.0 SP1
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Call Line
! /inp,lqs01_magn,inp
! Reset
finish
/clear,start

! Title
/title,LQS01, magnetic model
/filname,lqs01_magn,0
/UNITS,SI

/CONFIG,nproc,2

pi=acos(-1)

*afun,deg

/PREP7 

tref,293   

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!Cross-section parameters
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!Coil
*set,r1,45e-3
*set,r2,55.425e-3
*set,r3,65.850e-3

*set,block11,0
*set,block12,0
*set,block13,19.975
*set,block14,21.729

*set,block21,24.272
*set,block22,24.653
*set,block23,34.350
*set,block24,34.787

*set,block31,0
*set,block32,0
*set,block33,22.336
*set,block34,23.465

!Filler
*set,fillth,1.295e-3

!Pad
*set,padin,r3+fillth
*set,padth,30.480e-3
*set,padout,padin+padth
*set,rrod,12.7e-3
*set,rwash,25.4e-3
*set,rhole,15e-3
*set,xhole,61.148e-3
*set,yhole,61.148e-3
*set,rpadtie,9.525e-3/2
*set,xpadtie,71.082e-3
*set,ypadtie,33.452e-3

!Master
*set,pmasterth,14.3e-3
*set,pmasterin,padout-pmasterth
*set,pmastery,77.952e-3
*set,alkeyh,12.725e-3
*set,keyy,21.050e-3
*set,keyh,12.7e-3
*set,blady,37.309e-3
*set,bladh,26.416e-3
*set,slot,3.175e-3

!Yoke
*set,yokein,padout+4.225e-3
*set,yokeout,228e-3
*set,yblady,160.12e-3
*set,yalkeyy,196.008e-3
*set,yalkeyh,alkeyh
*set,ryoketie,22.225e-3/2
*set,xyoketie,158.828e-3
*set,yyoketie,75e-3
*set,ymasterth,pmasterth
*set,ymastery,pmastery

!Cylinder
*set,shellin,yokeout
*set,shellth,20e-3
*set,shellout,shellin+shellth

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!New coordinate systems
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

local,11,1,0,0,0,0,0,0
local,12,0,0,0,0,45,0,0
local,14,0,0,-3.175e-3*sqrt(2),0,45,0,0
                       
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!Elements, real constants and material properties
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

et,1,plane53
keyopt,1,1,0          ! AZ degree of freedom, no degrees of freedom
keyopt,1,5,2          ! NODAL FORCES,, nodal stress printout 

!Coil
mp,murx,1,1 

!Air, cylinder, pole, spacer and key   
mp,murx,2,1

!Yoke
tb,bh,3,,19 
/COM Old values, Iron BH-Curve from ANSYS Magnetics Manual
TBPT,,0,0
TBPT,,100,0.5
TBPT,,200,0.88
TBPT,,300,1.1
TBPT,,400,1.22
TBPT,,500,1.3
TBPT,,600,1.36
TBPT,,800,1.43
TBPT,,1000,1.47
TBPT,,1400,1.51
TBPT,,2000,1.55
TBPT,,4000,1.61
TBPT,,6000,1.66
TBPT,,8000,1.7
TBPT,,12000,1.76
TBPT,,16000,1.8
TBPT,,20000,1.84
TBPT,,30000,1.92
TBPT,,40000,1.97
!tb,bh,3,,20 
!TBPT,defi,0,0
!TBPT,,30.2394392,0.198
!TBPT,,175.070437,1.008
!TBPT,,246.690162,1.21
!TBPT,,326.267633,1.31
!TBPT,,596.831037,1.46
!TBPT,,1169.78883,1.59
!TBPT,,1591.54943,1.63
!TBPT,,2387.32415,1.682
!TBPT,,3183.09886,1.722
!TBPT,,4774.64829,1.774
!TBPT,,6366.19772,1.812
!TBPT,,7957.74715,1.842
!TBPT,,15915.4943,1.988
!TBPT,,31830.9886,2.151
!TBPT,,55704.2301,2.235
!TBPT,,79577.4715,2.272
!TBPT,,159154.943,2.38
!TBPT,,238732.415,2.481
!TBPT,,6366197.72,10.182

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!101: Coil, wedge, pole layer 1
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

numstr,kp,101
numstr,line,101
numstr,area,101

csys,1

k,101,r1,block11
k,102,r2,block12
k,103,r2,block13
k,104,r1,block14

k,105,r2,block34

k,106,r1,block21
k,107,r2,block22
k,108,r2,block23
k,109,r1,block24

k,110,r1,block24
k,111,r2,block23
k,112,r2,45
k,113,r1,45

csys,1

l,101,102,10
l,102,103,12
l,103,104,10
l,104,101,12

l,103,105,3
l,105,107,1
l,104,106,4

l,106,107,10
l,107,108,6
l,108,109,10
l,109,106,6

l,110,111,10
l,111,112,6
l,112,113,10
l,113,110,6


a,101,102,103,104
a,104,103,105,107,106
a,106,107,108,109
a,110,111,112,113

alls

asel,s,,,101
cm,block1,area
alls

asel,s,,,102
cm,wedge,area
alls

asel,s,,,103
cm,block2,area
alls

asel,s,,,104,
cm,pole1,area
alls

esize,1e-3

type,1                      
mat,1
cmsel,s,block1
cmsel,a,block2
aatt,1,,1
amesh,all
alls

type,1                      
mat,2
cmsel,s,wedge
aatt,2,,1
amap,102,104,103,107,106
alls

type,1                      
mat,2
cmsel,s,pole1
aatt,2,,1
amesh,all
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!201: Coil, pole layer 2
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

numstr,kp,201
numstr,line,201
numstr,area,201

csys,1

k,201,r2,block31
k,202,r3,block32
k,203,r3,block33
k,204,r2,block34
k,205,r2,block13

k,206,r2,block34
k,207,r3,block33
k,208,r3,0
kmove,208,14,u,0,0,1,r3,u,0
k,209,r3,45
k,210,r2,45
k,211,r2,block23
k,212,r2,block22

l,201,202,10
l,202,203,15
l,203,204,10
l,204,205,3
l,205,201,12

l,206,207,10
l,207,208,12
l,208,209,1
l,209,210,10
l,210,211,6
l,211,212,6
l,212,206,1

a,201,202,203,204,205
a,206,207,208,209,210,211,212

alls

asel,s,,,201
cm,block3,area
alls

asel,s,,,202,
cm,pole2,area
alls

esize,1e-3

type,1                      
mat,1
cmsel,s,block3
aatt,1,,1
amap,201,201,202,203,204
alls

type,1                      
mat,2
cmsel,s,pole2
aatt,2,,1
amap,202,206,207,209,210
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! Component
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

cmsel,s,block1
cmsel,a,block2
cmsel,a,block3
cm,coil,area
alls

cmsel,s,pole1
cmsel,a,pole2
cm,pole,area
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!301: Spacer/shim
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

numstr,kp,301
numstr,line,301
numstr,area,301

csys,1

k,301,r3,block32
k,302,r3+fillth,block32
k,303,r3+fillth,45
kmove,303,14,u,0,0,1,r3+fillth,u,0
k,304,r3,45
kmove,304,14,u,0,0,1,r3,u,0
k,305,r3,block33

csys,0
l,301,302,1
csys,1
l,302,303,27
csys,0
l,303,304,1
csys,1
l,304,305,12
l,305,301,15

a,301,302,303,304,305

asel,s,,,301
cm,filler,area
alls

type,1                      
mat,2
cmsel,s,filler
aatt,2,,1
amap,301,301,302,303,304
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!401: Pad
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

numstr,kp,401
numstr,line,401
numstr,area,401

csys,0

k,401,padin,0
k,402,pmasterin,0
k,403,pmasterin,pmastery-pmasterth
k,404,padout,pmastery
k,405,padout,0
kmove,405,14,u,0,0,0,padout,u,0
csys,1
k,406,padin,0
kmove,406,14,u,0,0,1,padin,u,0

csys,0
l,401,402,
l,402,403,
l,403,404,
l,404,405,
l,405,406,

csys,1
l,406,401,27

csys,0

a,401,402,403,404,405,406,

cyl4,xhole,yhole,rhole

asel,s,,,401,402,
asba,401,402,,dele,dele !area 403

cyl4,xpadtie,ypadtie,rpadtie	!area 401

asel,s,,,401,403,2
asba,403,401,,dele,keep !area 401,402

cyl4,xhole,yhole,rrod,225,rrod,405 !area 403

cyl4,xhole,yhole,rwash,225,rwash,405 !area 404

asel,s,,,402
cm,pad,area
alls

asel,s,,,403
cm,rod,area
alls

asel,s,,,404
cm,wash,area
alls

cmsel,s,rod
alls,below,area
aatt,2,,1
alls

type,1                      
mat,3
cmsel,s,pad
alls,below,area
aatt,3,,1
lesize,all,3e-3
esize,3e-3
amesh,all
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!451: Pmaster
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

numstr,kp,451
numstr,line,451
numstr,area,451

csys,0

k,451,pmasterin,0
k,452,padout-slot,0
k,453,padout-slot,alkeyh/2
k,454,padout,alkeyh/2
k,455,padout,keyy
k,456,padout-slot,keyy
k,457,padout-slot,keyy+keyh
k,458,padout,keyy+keyh
k,459,padout,blady
k,460,padout-slot,blady
k,461,padout-slot,blady+bladh
k,462,padout,blady+bladh
k,463,padout,pmastery
k,464,pmasterin,pmastery-pmasterth

l,451,452,
l,452,453,
l,453,454,
l,454,455,
l,455,456,
l,456,457,
l,457,458,
l,458,459,
l,459,460,
l,460,461,
l,461,462,
l,462,463,
l,463,464,
l,464,451,

a,451,452,453,454,455,456,457,458,459,460,461,462,463,464

asel,s,,,451
cm,pmaster,area
alls

type,1                      
mat,3
cmsel,s,pmaster
alls,below,area
aatt,3,,1
lesize,all,3e-3
esize,3e-3
amesh,all
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!501: Key
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

numstr,kp,501
numstr,line,501
numstr,area,501

csys,0

k,501,padout-slot,keyy
k,502,padout,keyy
k,503,yokein,keyy
k,504,yokein+slot,keyy
k,505,yokein+slot,keyy+keyh
k,506,yokein,keyy+keyh
k,507,padout,keyy+keyh
k,508,padout-slot,keyy+keyh

l,501,502
l,502,503,
l,503,504
l,504,505
l,505,506
l,506,507
l,507,508
l,508,501

a,501,502,503,504,505,506,507,508

asel,s,,,501
cm,key,area
alls

type,1                      
mat,3
cmsel,s,key
alls,below,area
aatt,3,,1
lesize,all,3e-3
esize,3e-3
amesh,all
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!601: Yoke
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

numstr,kp,601
numstr,line,601
numstr,area,601

csys,0

k,601,yokein+ymasterth,0
k,602,yokeout,0
k,603,yokeout,0
kmove,603,14,u,0,0,1,yokeout,u,0

csys,14

k,604,yalkeyy+yalkeyh,0
k,605,yalkeyy+yalkeyh,-slot
k,606,yalkeyy,-slot
k,607,yalkeyy,0
k,608,yblady+bladh,0
k,609,yblady+bladh,-slot
k,610,yblady,-slot
k,611,yblady,0

csys,0
k,612,yokein,0
kmove,612,14,u,0,0,0,yokein,u,0
k,613,yokein,ymastery
k,614,yokein+ymasterth,ymastery-ymasterth

l,601,602,

csys,1

l,602,603,

csys,0

l,603,604,
l,604,605,
l,605,606,
l,606,607,
l,607,608,
l,608,609,
l,609,610,
l,610,611,
l,611,612,
l,612,613,
l,613,614,
l,614,601,

a,601,602,603,604,605,606,607,608,609,610,611,612,613,614

cyl4,xyoketie,yyoketie,ryoketie	!area 602

asel,s,,,601,602
asba,601,602,,dele,keep !area 602,603

asel,s,,,603
cm,yoke,area
alls

type,1                      
mat,3
cmsel,s,yoke
alls,below,area
aatt,3,,1
lsel,r,loc,x,yokein-0.1e-3,yokein+ymasterth+0.1e-3
lesize,all,3e-3
alls,below,area
esize,6e-3
amesh,all
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!651: Ymaster
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

numstr,kp,651
numstr,line,651
numstr,area,651

csys,0

k,651,yokein+slot,0
k,652,yokein+slot,alkeyh/2
k,653,yokein,alkeyh/2
k,654,yokein,keyy
k,655,yokein+slot,keyy
k,656,yokein+slot,keyy+keyh
k,657,yokein,keyy+keyh
k,658,yokein,blady
k,659,yokein+slot,blady
k,660,yokein+slot,blady+bladh
k,661,yokein,blady+bladh
k,662,yokein,ymastery
k,663,yokein+ymasterth,ymastery-ymasterth
k,664,yokein+ymasterth,0

l,651,652,
l,652,653,
l,653,654,
l,654,655,
l,655,656,
l,656,657,
l,657,658,
l,658,659,
l,659,660,
l,660,661,
l,661,662,
l,662,663,
l,663,664,
l,664,651,

a,651,652,653,654,655,656,657,658,659,660,661,662,663,664

asel,s,,,651
cm,ymaster,area
alls

type,1                      
mat,3
cmsel,s,ymaster
alls,below,area
aatt,3,,1
lsel,r,loc,x,yokein-0.1e-3,yokein+ymasterth+0.1e-3
lesize,all,3e-3
alls,below,area
esize,6e-3
amesh,all
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!701: Shell
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

numstr,kp,701
numstr,line,701
numstr,area,701

csys,1

k,701,shellin,0
k,702,shellout,0
k,703,shellout,0
kmove,703,14,u,0,0,1,shellout,u,0
k,704,shellout,45
k,705,shellin,45
k,706,shellin,45
kmove,706,14,u,0,0,1,shellin,u,0

l,701,702,
l,702,703,
l,703,704,
l,704,705,
l,705,706,
l,706,701,

csys,1

a,701,702,703,704,705,706,

asel,s,,,701
cm,shell,area
alls

type,1                      
mat,2
cmsel,s,shell
alls,below,area
aatt,2,,1
csys,1
lsel,r,loc,x,shellin
lesize,all,6e-3
csys,0
alls,below,area
esize,6e-3
amap,701,701,702,704,705
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!801: Air
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

numstr,kp,801
numstr,line,801
numstr,area,801

csys,1

k,801,0,0
k,802,r1,45

csys,0
k,803,yokein,0
kmove,803,12,u,0,0,0,yokein,u,0

csys,1
l,801,802,
l,802,109,6
l,801,101

csys,0

a,801,802,109,106,104,101	!area 801

a,452,651,652,653,654,455,454,453,	!area 802

a,657,658,659,660,661,662,612,405,463,462,461,460,459,458	!area 803

a,209,208,406,412,410,407,411,405,612,803	!area 804

a,803,612,611,610,609,608,607,606,605,604,706,705	!area 805

asel,s,,,801,805
asel,a,,,401
asel,a,,,602
cm,air,area
alls

type,1                      
mat,2
cmsel,s,air
alls,below,area
aatt,2,,1

asel,s,,,801,
esize,3e-3
amesh,all
alls

asel,s,,,401
alls,below,area
esize,6e-3
amesh,all
alls


asel,s,,,802,803
esize,3e-3
amesh,all
alls

asel,s,,,804
alls,below,area
esize,3e-3
amesh,all
alls

asel,s,,,805
alls,below,area
esize,6e-3
amesh,all
alls

asel,s,,,602
alls,below,area
esize,6e-3
amesh,all
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!Compute the current densities
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

*set,Io,-13300  ! A/cable                    

cmsel,s,block1
aplot
asum
*get,a1,area,,area
*set,j1,(12*Io/a1)
alls

cmsel,s,block2
aplot
asum
*get,a2,area,,area
*set,j2,(6*Io/a2)
alls

cmsel,s,block3
aplot
asum
*get,a3,area,,area
*set,j3,(16*Io/a3)
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!Apply the current densities
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

cmsel,s,block1               
esla
bfe,all,js,3,(j1)
alls

cmsel,s,block2              
esla
bfe,all,js,3,(j2)
alls

cmsel,s,block3           
esla
bfe,all,js,3,(j3)
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!Coupling the coincident nodes
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

cpintf,all,1e-4,
alls

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!Define the boundary conditions
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

csys,1
nsel,s,loc,y,45
d,all,az,0
alls

et,2,infin9
alls
csys,1
lsel,s,loc,x,shellout
type,2
lmesh,all
alls  

finish

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!Solution
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

/solu

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!Comands for solving
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

outres,all,last
ANTYPE,STATIC           ! static model
NROPT,FULL              ! Use full Newton-Rapshon method
EQSLV,FRONT             ! Frontal direct equation
NEQIT,20                ! 20 iterations per substep
KBC,1                   ! Loads are stp changed
AUTOTS,ON               ! automatic stepping and bisection
NSUBST,10               ! number of substeps
CNVTOL,CSG,,1E-6        ! tolerances equilibrium iterations

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!Solving
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

solve

finish

/post1

csys,0
rsys,0

alls
cmsel,s,block1
cmsel,a,block2
alls,below,area
PLNSOL,B,SUM,0, 
*GET,bmax1,PLNSOL,0,max
alls

alls
cmsel,s,block3
alls,below,area
PLNSOL,B,SUM,0, 
*GET,bmax2,PLNSOL,0,max
alls

etable,erase
alls
cmsel,s,block1
alls,below,area
csys,1
rsys,1
etable,01fr,fmag,x 
etable,01ftheta,fmag,y 
csys,0
rsys,0
etable,01fx,fmag,x 
etable,01fy,fmag,y 
/output,lqs01_magn_results_output,txt,,
ssum
/output,

etable,erase
alls
cmsel,s,block2
alls,below,area
csys,1
rsys,1
etable,02fr,fmag,x 
etable,02ftheta,fmag,y 
csys,0
rsys,0
etable,02fx,fmag,x 
etable,02fy,fmag,y 
/output,lqs01_magn_results_output,txt,,append
ssum
/output,

etable,erase
alls
cmsel,s,block1
cmsel,a,block2
alls,below,area
csys,1
rsys,1
etable,012fr,fmag,x 
etable,012ftheta,fmag,y 
csys,0
rsys,0
etable,012fx,fmag,x 
etable,012fy,fmag,y 
/output,lqs01_magn_results_output,txt,,append
ssum
/output,

etable,erase
alls
cmsel,s,block3
alls,below,area
csys,1
rsys,1
etable,03fr,fmag,x 
etable,03ftheta,fmag,y 
csys,0
rsys,0
etable,03fx,fmag,x 
etable,03fy,fmag,y 
/output,lqs01_magn_results_output,txt,,append
ssum
/output,

etable,erase
alls
cmsel,s,block1
cmsel,a,block2
cmsel,a,block3
alls,below,area
csys,1
rsys,1
etable,0123fr,fmag,x 
etable,0123ftheta,fmag,y 
csys,0
rsys,0
etable,0123fx,fmag,x 
etable,0123fy,fmag,y 
/output,lqs01_magn_results_output,txt,,append
ssum
/output,

alls
nsel,s,loc,y,0
nsel,r,loc,x,(22.5e-3)-(2e-3),(22.5e-3)+(2e-3)
esln,s,0
*get,emin,elem,,num,min
alls
esel,s,,,emin
nsle,r,1
nsel,r,loc,y,0
*get,nmin,node,,num,min
nsel,s,,,nmin
*get,xnode,node,nmin,loc,x
*get,bnode,node,nmin,b,y
*set,grad,bnode/xnode

/output,lqs01_magn_results_output,txt,,append
*MSG, INFO,
***************************
*MSG, INFO, Bmax1, 'T'
Peak field inner = %G %C
*MSG, INFO, Bmax2, 'T'
Peak field outer = %G %C
*MSG, INFO, Io, 'A'
Current = %G %C
*MSG, INFO, grad, 'T/m'
Gradient = %G %C
/output,

csys,0
rsys,0
cmsel,s,coil
alls,below,area
PLNSOL,B,SUM,0, 

alls

save

/delete,,esav,
/delete,,emat,
/delete,,dbb,
/delete,,osav,
/delete,,tri,
